#ifndef DOXY_SKIP
#include "ibi-preprocess.h"
#endif

!> @file
!> @brief Moves atoms in box according to LF algorithm
!> @details Called from NEW_LOOP.f90

SUBROUTINE MOVE()

  USE VAR
  USE OMP_LIB 

  IMPLICIT NONE
  
  INTEGER :: I, TI, A
  REAL*4 :: LCFAC_ATOM, LCFAC_BEAD, INV_MASS, REG_MASS
  REAL*4, DIMENSION(3) :: VXYZ_I
  REAL*4, DIMENSION(2) :: PT11K, PT22K, PT33K
  REAL*4, DIMENSION(2) :: PT12K, PT23K, PT13K

  EK = 0.0

  PT11K = 0.0
  PT22K = 0.0
  PT33K = 0.0

  PT12K = 0.0
  PT23K = 0.0
  PT13K = 0.0

  IF ((ENSEMBLE == 1).OR.(ENSEMBLE == 2)) THEN !If NVT or NPT
     LCFAC_ATOM = SQRT(1.0+FAC*((TEMP_IN/TEMP_ATOM)-1.0))
     LCFAC_BEAD = SQRT(1.0+FAC*((TEMP_IN/TEMP_BEAD)-1.0)) 
  ELSE
     LCFAC_ATOM = 1.0
     LCFAC_BEAD = 1.0
  END IF

  DO A=1,NUMATOMS
     I = ATOM(A)
     TI = ITYPE(I)
     REG_MASS = MASS(TI)
     INV_MASS = INVMASS(TI)

     VXYZ_I(:) = VXYZ(:,I)

     VXYZ(:,I) = (VXYZ_I(:) + DT * FXYZ(:,I) * INV_MASS) * LCFAC_ATOM

     SXYZ(:,I) = SXYZ(:,I) + DT * VXYZ(:,I)

     VTXYZ(:,I) = 0.5 * (VXYZ(:,I) + VXYZ_I(:))

     VXYZ_I(:) = VTXYZ(:,I)
  
     PT11K(1) = PT11K(1) + REG_MASS *VXYZ_I(1) * VXYZ_I(1)
     PT22K(1) = PT22K(1) + REG_MASS *VXYZ_I(2) * VXYZ_I(2)  
     PT33K(1) = PT33K(1) + REG_MASS *VXYZ_I(3) * VXYZ_I(3)

     PT12K(1) = PT12K(1) + REG_MASS * VXYZ_I(1) * VXYZ_I(2)
     PT13K(1) = PT13K(1) + REG_MASS * VXYZ_I(1) * VXYZ_I(3)
     PT23K(1) = PT23K(1) + REG_MASS * VXYZ_I(2) * VXYZ_I(3)     
  END DO

  DO A=1,NUMBEADS
     I = BEAD(A)
     TI = ITYPE(I)
     REG_MASS = MASS(TI)
     INV_MASS = INVMASS(TI)

     VXYZ_I(:) = VXYZ(:,I)

     VXYZ(:,I) = (VXYZ_I(:) + DT * FXYZ(:,I) * INV_MASS) * LCFAC_BEAD

     SXYZ(:,I) = SXYZ(:,I) + DT * VXYZ(:,I)

     VTXYZ(:,I) = 0.5 * (VXYZ(:,I) + VXYZ_I(:))

     VXYZ_I(:) = VTXYZ(:,I)
  
     PT11K(2) = PT11K(2) + REG_MASS * VXYZ_I(1) * VXYZ_I(1)
     PT22K(2) = PT22K(2) + REG_MASS * VXYZ_I(2) * VXYZ_I(2)  
     PT33K(2) = PT33K(2) + REG_MASS * VXYZ_I(3) * VXYZ_I(3)

     PT12K(2) = PT12K(2) + REG_MASS * VXYZ_I(1) * VXYZ_I(2)
     PT13K(2) = PT13K(2) + REG_MASS * VXYZ_I(1) * VXYZ_I(3)
     PT23K(2) = PT23K(2) + REG_MASS * VXYZ_I(2) * VXYZ_I(3)  
  END DO

  PT11 = PT11 + SUM(PT11K)
  PT22 = PT22 + SUM(PT22K)
  PT33 = PT33 + SUM(PT33K)

  PT12 = PT12 + SUM(PT12K)
  PT13 = PT13 + SUM(PT13K)
  PT23 = PT23 + SUM(PT23K)

!  EK = 0.5 * (SUM(PT11K) + SUM(PT22K) + SUM(PT33K))
  EK(1) = 0.5 * (PT11K(1) + PT22K(1) + PT33K(1))
  EK(2) = 0.5 * (PT11K(2) + PT22K(2) + PT33K(2))

  TEMP = SUM(EK) * MKTEMP
  TEMP_ATOM = EK(1) * MKTEMP_ATOM
  TEMP_BEAD = EK(2) * MKTEMP_BEAD

  VOLUME = BOX(1) * BOX(2) * BOX(3)
  INV_VOLUME = 1.0 / VOLUME

  PT11 = PT11 * INV_VOLUME
  PT22 = PT22 * INV_VOLUME
  PT33 = PT33 * INV_VOLUME

  PT12 = PT12 * INV_VOLUME
  PT13 = PT13 * INV_VOLUME
  PT23 = PT23 * INV_VOLUME

  RETURN
END SUBROUTINE MOVE
